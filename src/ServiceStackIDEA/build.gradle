group 'net.servicestack'
version '1.0.8'

apply plugin: 'java'

repositories {
    mavenCentral()
}

def IDEA_SDK_NAME = 'IntelliJ IDEA Community Edition IC-141.1532.4'

configurations {
    ideaSdk
    bundle // dependencies bundled with the plugin
    provided
    compile.extendsFrom provided
}


dependencies {
    ideaSdk fileTree(dir: 'lib/sdk/', include: ['*/lib/*.jar','*/plugins/maven/lib/*.jar'])
    provided fileTree(dir: 'lib/sdk/', include: ['*/lib/*.jar','*/plugins/maven/lib/*.jar'])
    compile configurations.ideaSdk
    compile configurations.bundle
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'org.apache.maven:maven-model:3.0.5'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.intellij:forms_rt:6.0.5'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

jar {
    dependsOn configurations.runtime
    from {
        (configurations.compile - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

task extractIdeaSdk(type: Copy) {
    def ideaSdkZipPath;
    if(System.getProperty('ideaSdkZip')) {
        ideaSdkZipPath = System.getProperty('ideaSdkZip') as String
    } else {
        ideaSdkZipPath = '/lib/idea-sdk.zip';
    }
    def zipFile = file(ideaSdkZipPath)
    def outputDir = file("lib/sdk/idea-ce-14.0.4")

    if(!outputDir.exists()) {
        from zipTree(zipFile)
        into outputDir
    }
}

test {
    // Avoid parallel execution, since the IntelliJ boilerplate is not up to that
    maxParallelForks = 1
}

task dist(type: Zip, dependsOn: [jar, test]) {
    from configurations.bundle
    from jar.archivePath
    rename { f -> "lib/${f}" }
    into project.name
    baseName project.name
}

compileJava.dependsOn extractIdeaSdk
build.dependsOn dist

// ========= Workspace setup tasks ========= //

apply plugin: 'idea'

idea {
    project {
        languageLevel = '1.6'
        jdkName = IDEA_SDK_NAME

        ipr {
            withXml {
                it.node.find { node ->
                    node.@name == 'ProjectRootManager'
                }.'@project-jdk-type' = 'IDEA JDK'

                logger.warn "=" * 71
                logger.warn " Configured IDEA JDK '${jdkName}'."
                logger.warn " Make sure you have it configured IntelliJ before opening the project!"
                logger.warn "=" * 71
            }
        }
    }

    module {
        scopes.COMPILE.minus = [ configurations.ideaSdk ]

        iml {
            beforeMerged { module ->
                module.dependencies.clear()
            }
            withXml {
                it.node.@type = 'PLUGIN_MODULE'
                //  <component name="DevKit.ModuleBuildProperties" url="file://$MODULE_DIR$/src/main/resources/META-INF/plugin.xml" />
                def cmp = it.node.appendNode('component')
                cmp.@name = 'DevKit.ModuleBuildProperties'
                cmp.@url = 'file://$MODULE_DIR$/src/main/resources/META-INF/plugin.xml'
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
