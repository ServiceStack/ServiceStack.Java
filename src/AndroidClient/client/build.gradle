apply plugin: 'com.jfrog.bintray'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'


dependencies {
    compile 'com.google.code.gson:gson:2.4'
    compile 'org.apache.httpcomponents:httpmime:4.2.3'
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-core:1.9.5'
}

version = "1.0.23"
group = "net.servicestack"                                        // Maven Group ID for the artifact
String packageId = "client"
String groupID = group
String versionNum = version

String siteUrl = 'https://github.com/ServiceStack/ServiceStack.Java'      // Homepage URL of the library
String gitUrl = 'https://github.com/ServiceStack/ServiceStack.Java.git'   // Git repository URL

Properties properties = System.properties;
if(properties.getProperty("bintray.user", null) == null) {
    properties.load(project.file('./local.properties').newDataInputStream())
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'jar'
                groupId groupID
                artifactId packageId
                version versionNum
                // Add your description here
                name 'ServiceStack.Client'
                description 'A client library to call your ServiceStack webservices.'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The BSD 3-Clause License'
                        url 'https://servicestack.net/bsd-license.txt'
                    }
                }

                developers {
                    developer {
                        id 'mythz'
                        name 'Demis Bellot'
                        email 'team@servicestack.net'
                    }
                    developer {
                        id 'layoric'
                        name 'Darren Reid'
                        email 'team@servicestack.net'
                    }
                }
                scm {
                    connection 'https://github.com/ServiceStack/ServiceStack.Java'
                    developerConnection 'https://github.com/ServiceStack/ServiceStack.Java'
                    url 'https://github.com/ServiceStack/ServiceStack.Java'
                }
            }
        }.writeTo('pom.xml')
    }
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apiKey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        userOrg = 'servicestack'
        name = "ServiceStack.Client"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["BSD 3-Clause"]
        publish = properties.getProperty('bintray.publish') == 'true'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}