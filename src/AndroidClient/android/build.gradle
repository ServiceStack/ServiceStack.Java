apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.0.48"

dependencies {
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation fileTree(include: '*.jar', dir: 'libs')
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName version

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

Properties properties = System.properties;
if(properties.getProperty("bintray.user", null) == null) {
    properties.load(project.file('./local.properties').newDataInputStream())
}

def siteUrl = 'https://github.com/ServiceStack/ServiceStack.Java'      // Homepage URL of the library
def gitUrl = 'https://github.com/ServiceStack/ServiceStack.Java.git'   // Git repository URL
group = "net.servicestack"                                        // Maven Group ID for the artifact

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apiKey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "ServiceStack.Android"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["BSD 3-Clause"]
        publish = properties.getProperty('bintray.publish') == 'true'
        userOrg = 'servicestack'
    }
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'ServiceStack.Android'
                description 'A client library to call your ServiceStack webservices for Android clients.'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The BSD 3-Clause License'
                        url 'https://servicestack.net/bsd-license.txt'
                    }
                }

                developers {
                    developer {
                        id 'mythz'
                        name 'Demis Bellot'
                        email 'team@servicestack.net'
                    }
                    developer {
                        id 'layoric'
                        name 'Darren Reid'
                        email 'team@servicestack.net'
                    }
                }
                scm {
                    connection 'https://github.com/ServiceStack/ServiceStack.Java'
                    developerConnection 'https://github.com/ServiceStack/ServiceStack.Java'
                    url 'https://github.com/ServiceStack/ServiceStack.Java'

                }
            }
        }
    }
}

repositories {
    mavenCentral()
}
